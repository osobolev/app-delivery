subprojects {
    apply plugin: 'java-library'

    group = 'com.github.osobolev.app-delivery.sample'
    version = '5.0'

    sourceCompatibility = '8'

    compileJava.options.encoding = 'UTF-8'

    sourceSets.main.java.srcDirs = ['src']

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

project(':sample_dto') {

    configurations {
        sqlg
    }

    dependencies {
        implementation 'com.github.osobolev.sqlg3:sqlg3-core:1.0'

        sqlg 'com.github.osobolev.sqlg3:sqlg3-preprocess:1.0'
        sqlg 'com.h2database:h2:1.4.191'
    }

    task preprocess {
        doLast {
            ant.taskdef(name: 'sqlg', classname: 'sqlg3.preprocess.ant.Preprocess', classpath: configurations.sqlg.asPath)
            ant.sqlg(classpath: configurations.sqlg.asPath,
                     driverClass: 'org.h2.Driver',
                     url: 'jdbc:h2:mem:', user: '', password: '',
                     srcRoot: '../sample_db/src', destRoot: 'src',
                     encoding: compileJava.options.encoding) {
            }
        }
    }
    compileJava.dependsOn(preprocess)
}

project(':sample_db') {

    dependencies {
        implementation 'com.github.osobolev.sqlg3:sqlg3-runtime:1.0'
        implementation project(':sample_dto')
    }
}

project(':sample_client') {
    dependencies {
        implementation 'com.github.osobolev.app-delivery:apploader-client:5.0'
        implementation 'com.github.osobolev.sqlg3:sqlg3-remote-client:1.0'
        implementation project(':sample_dto')
    }
}

project(':sample_war') {
    
    apply plugin: 'war'

    configurations {
        webapploader
        webdistr
        webfiles
    }

    dependencies {
        implementation 'com.github.osobolev.sqlg3:sqlg3-remote-server:1.0'
        implementation 'com.github.osobolev.app-delivery:server-war:5.0'
        runtimeOnly project(':sample_db')

        webfiles 'com.github.osobolev.app-delivery:server-files:5.0@war'
        webapploader 'com.github.osobolev.app-delivery:apploader:5.0'
        webdistr project(':sample_client')
    }

    task createList {
        doLast {
            def list = ['corejar=apploader.jar']
            configurations.webdistr.each {
                list.add("jar=distr/${it.name}")
            }
            list.add('mainClass=sample.Client')
            def file = file('build/list/sample_jars.list')
            file.parentFile.mkdirs()
            def eoln = '\n'
            file.setText(list.join(eoln) + eoln, compileJava.options.encoding)
        }
    }
    war.dependsOn(createList)

    war {
        archiveFileName = 'sample_app.war'
        webAppDirName = 'webapp'

        from(configurations.webdistr) {
            into 'distr'
        }
        from(configurations.webapploader) {
            rename('apploader-.*\\.jar', 'apploader.jar')
        }
        from('build/list')
        from(zipTree(configurations.webfiles.find({true})))
    }
}
