subprojects {
    group = 'com.github.osobolev.app-delivery'
    version = '3.0'

    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    compileJava.options.compilerArgs << '-Xlint:-options,deprecation,unchecked'
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    sourceSets.main.java.srcDirs = ['src']
    
    jar.manifest.attributes('Implementation-Version': version)
}

project(':apploader-client') {
    dependencies {
        compile project(':apploader-common')
    }
}

project(':apploader') {
    dependencies {
        compileOnly project(':apploader-client')
    }

    jar {
        from {
            configurations.compileOnly.collect { it.isDirectory() ? it : zipTree(it) }
        }
        manifest.attributes('Main-Class': 'apploader.AppLoader')
    }
}

project(':server-core') {
    dependencies {
        compile 'com.github.osobolev.sqlg2:sqlg2-runtime:4.0'
    }
}

project(':server-install') {
    dependencies {
        compile project(':apploader-common')
    }
}

project(':server-http') {
    dependencies {
        compile 'javax.servlet:javax.servlet-api:3.1.0'
        compile 'com.github.osobolev.sqlg2:sqlg2-runtime:4.0'
        compile project(':server-install')
    }
}

project(':server-jetty') {
    dependencies {
        compile 'org.eclipse.jetty:jetty-servlet:9.3.15.v20161220'
        compile project(':server-core')
        compile project(':server-http')
    }
}

project(':server-service') {
    dependencies {
        compile 'org.boris.winrun4j:winrun4j:0.4.5'
        compile project(':server-jetty')
    }
}

project(':server-war') {
    dependencies {
        compile project(':server-core')
        compile project(':server-http')
    }
}

project(':server-desktop') {
    dependencies {
        compile project(':server-jetty')
    }
}

project(':server-files') {
    apply plugin: 'war'

    jar.onlyIf { false }

    webAppDirName = 'webapp'
}
